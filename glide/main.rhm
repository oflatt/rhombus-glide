#lang rhombus


import:
  rhombus/meta open
  pict open
  pict/text open
  pict/rhombus open
  rhombus/measure
  rhombus/runtime_path
  rhombus/fixnum expose:
    - as fm
  gui expose:
    ~>
    <~
  "traverse_rhombus.rhm" open
  "ui.rhm" open
  "stx_helpers.rhm" open
  "wrap_stx.rhm" open
  "elements.rhm" open

export:
  glide
  pad
  Rendered
  draw_slide
  scale_to_fit
  all_from("elements.rhm")

fun set_width(pict, width):
  let current_width = pict.width
  pict.pad(~right: width - current_width)

fun set_height(pict, height):
  let current_height = pict.height
  pict.pad(~bottom: height - current_height)


enum Action:
  pad
  none

class InputState(action, downMouseX, downMouseY, currentX, currentY)

fun distance(x1, y1, x2, y2):
  math.sqrt((x1 - x2) * (x1 - x2) + (y1 - y2) * (y1 - y2))

// screen is a pict for the current screen
// tabs is a list of lists
// each list contains picts for the tab
class Rendered(selected_slide, tabs)


// inputState is an InputState object
// code is the current code for the body of the function
// old_code is the code that we loaded in from the file
// ui is a UI object
class Glide(inputState, code, old_code, srcloc, ui):
  method code_with_action() :: Syntax:
    if this.inputState.action == Action.pad:
    | block:
        cond 
        | distance(this.inputState.downMouseX, this.inputState.downMouseY, this.inputState.currentX, this.inputState.currentY) < 10:
            this.code
        | ~else:
            add_padding_to(this.code, this.ui.selected, this.inputState.currentX - this.inputState.downMouseX, this.inputState.currentY - this.inputState.downMouseY)
    | this.code

  method current_rendered(canvas_size) :: Rendered:
    let code = this.code_with_action()
    let slides = get_slides(code)
    let tabs = get_tabs(code)
    let room_for_tabs = canvas_size.width * ui.tab_screen_width
    let room_for_slides = canvas_size.width - room_for_tabs

    // TODO don't evaluate slides and tabs multiple times like we do here
    let tabs:
      eval('block:
              $(code)
              $tabs ++ [$slides]')
    
    let slide:
      scale_to_fit(
        draw_inset_slide(tabs[this.ui.current_slide_index.tab][this.ui.current_slide_index.index]),
        [room_for_slides, canvas_size.height])
        .pad(~left: room_for_tabs)
    Rendered(slide, tabs)

  method pad_action_start(current_rendered, ev):
    cond
    | ev.kind == gui.MouseEvent.Kind.left_down:
        // if we're clicking an already selected pict
        let selected = find_pict_from_gpi(current_rendered.selected_slide, this.ui.selected)
        if is_pict_clicked(current_rendered.selected_slide, selected, ev):
        | this with (inputState = InputState(Action.pad, ev.x, ev.y, ev.x, ev.y))
        | this with (inputState = InputState(Action.none, ev.x, ev.y, ev.x, ev.y))
    | ~else: this

  method pad_action_end_release(current_rendered, ev):
    // if the distance traveled is less than 10, select a new pict
    if distance(this.inputState.downMouseX, this.inputState.downMouseY, ev.x, ev.y) < 10:
    | block:
        let new_selected:
          find_clicked_pict(current_rendered.selected_slide, ev, this.ui.selected)
        println("new selected: " +& new_selected)
        if new_selected:
        | this with (ui = (this.ui with (selected = new_selected.metadata()["GlidePictIdentity"])))
        | this with (ui = (this.ui with (selected = #false)))
    | block:
        // replace code with the newest code_with_action()
        let updated = this.code_with_action()
        this with (code = updated)

  method pad_action_end(current_rendered, ev):
    cond
    | ev.kind == gui.MouseEvent.Kind.left_up:
        let new_input_state = this.inputState with (action = Action.none)
        this.pad_action_end_release(current_rendered, ev) with (inputState = new_input_state)
    | ~else: this

  method update_input_state(ev):
    let new_input_state = this.inputState with (currentX = ev.x, currentY = ev.y)
    this with (inputState = new_input_state)


  method on_mouse(ev, canvas_size):
    let updated = this.update_input_state(ev)
    let current_rendered:
      updated.current_rendered(canvas_size)
  
    let with_input:
      updated.pad_action_start(current_rendered, ev).pad_action_end(current_rendered, ev)

    let with_ui_event:
      with_input with (ui = with_input.ui.on_mouse(ev, current_rendered, canvas_size))

    // when a new slide is needed, the UI sets the current slide out of bounds
    // add a new slide here to fix this problem
    let with_new_slide:
      if with_ui_event.ui.current_slide_index.index == current_rendered.tabs[with_ui_event.ui.current_slide_index.tab].length:
      | with_ui_event.add_new_slide()
      | with_ui_event

    with_new_slide

  method onkey(ev):
    let with_save = if ev.code == Char"s" && gui.KeyEvent.Down.control in ev.downs:
    | do_save(this)
    | this

    if gui.KeyEvent.Key.escape == ev.code:
    | with_save with (ui = (with_save.ui with (selected = #false)))
    | with_save

  method add_new_slide():
    let fresh_slide_name = get_fresh_slide_name(this.code)
    let new_code = new_slide_ref(add_new_func(this.code, fresh_slide_name), fresh_slide_name)
    this with (code = new_code)

fun repeat_str(str, amt):
  def mutable res = ""
  for (i in 0..amt):
    res := res +& str
  res

fun indent(code, amt):
  let lines = String.split(code, "\n")
  let indented = (for List:
                    each line in lines
                    repeat_str(" ", amt) +& line)
  String.join(indented, "\n")


// todo why all these constants? no idea
fun do_save(glide):
  let srcloc = glide.srcloc
  
  let source = filesystem.read_string(srcloc.source)
  let before = String.substring(source, 0, srcloc.position-3)
  let after:
    if srcloc.position + srcloc.span >= String.length(source)
    | ""
    | String.substring(source, srcloc.position + srcloc.span-1, String.length(source))

  let identities_removed = remove_pict_identities(glide.code)
  let new_middle = indent(identities_removed.to_source_string(), 2)

  let new_source = before +& new_middle +& after
  
  println("saving to: " +& srcloc.source)
  filesystem.write_string(srcloc.source, new_source, ~exists: #'replace)

  // now update the srcloc of the glide's code
  let new_src_loc = Srcloc(srcloc.source, srcloc.line, srcloc.column, srcloc.position, String.length(new_middle)-2)
  glide with (old_code = glide.code, srcloc = new_src_loc)


fun save_icon():
  overlay(
    rectangle(~width: 50, ~height: 50, ~fill: "gray", ~line: "black", ~line_width: 3),
    stack(text("save"),
          text("ctrl+s"))
  )


fun draw_for(state):
  // draw a glide state to the dc
  fun draw(dc, glide):
    let current_rendered = glide.current_rendered(dc.size)
    current_rendered.selected_slide.draw(dc)
    glide.ui.draw(dc, glide.inputState, current_rendered)

    // if we need to save because data.code is different from data.old_code, draw save icon
    if glide.code != glide.old_code:
    | let savePict = scale_to_fit(save_icon(), [dc.size.width * 0.1, dc.size.height * 0.1]).pad(~top: dc.size.height * 0.9, ~left: dc.size.width * 0.9)
      savePict.draw(dc)
    | #false
  draw
  


fun on_mousefor(state):
  fun on_mouse(ev :: gui.MouseEvent, area):
    state <~ fun (old): old.on_mouse(ev, area.client_size)
  on_mouse

fun onkeyfor(state):
  fun onkey(ev :: gui.KeyEvent, area):
    state <~ fun (old): old.onkey(ev)
  onkey


fun edit_slide(stx, config_stx):
  let tabs_stx = stx_to_list(get_tabs(stx))
  let num_tabs = tabs_stx.length() + 1
  let tabs:
    for List:
      each _i in 0..num_tabs
      Tab(0)

  let ui = UI(#false, eval(config_stx), config_stx, tabs, 0.15, num_tabs-1, SlideIndex(num_tabs-1, 0))
  let with_ids = add_pict_identities(stx)
  let state = gui.Obs(Glide(InputState(Action.none, 0, 0, 0, 0), with_ids, with_ids, with_ids.srcloc(), ui))

  let win = gui.Window(gui.Canvas(state, draw_for(state), ~mouse: on_mousefor(state), ~key: onkeyfor(state)), ~size: [initialScreenW, initialScreenH])
  gui.render(win)

fun is_glide_call(stx):
  match stx:
  | '$name($args, ...)':
      name.to_source_string() == "rectangle" || name.to_source_string() == "beside"
  | _: #false


// returns stx, but whereever it finds a glide call,
// it wraps it in a GPI__ call with a fresh number
fun add_pict_identities(stx) :: Syntax:
  def mutable current_id = 0
  traverse_stx(stx,
               fun (stx):
                 if is_glide_call(stx):
                 | block:
                     let current = current_id
                     current_id := current_id + 1
                     Syntax.relocate_group(
                       'GPI__($stx, $current)',
                       stx)
                 | stx)

fun remove_pict_identities(stx):
  traverse_stx(stx,
               fun (stx):
                 match stx:
                 | 'GPI__($pict, $n)':
                     pict
                 | _: stx)

meta:
  fun glide(syntax, config):
    '«edit_slide('$(syntax)', '$(config)')»'

defn.macro 'glide $config:
              $stx':
  glide(stx, config)
