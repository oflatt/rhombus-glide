#lang rhombus

import:
  rhombus/meta open
  pict open
  pict/text open
  pict/rhombus open
  gui expose:
    ~>
    <~

import:
  "stx_helpers.rhm" open

let initialScreenW = 1700
let initialScreenH = 800 

let aspect_ratio = 16 / 9

fun scale_to_fit(pict, size):
  let scaleW = size[0] / pict.width
  let scaleH = size[1] / pict.height
  let scale = math.min(scaleW, scaleH)
  pict.scale(scale)

fun scale_to_width(pict, width):
  let scale = width / pict.width
  pict.scale(scale)

fun mini_slide(pict):
  rectangle(~around: pict, ~line_width: 4).pad(~left: 0.025 * initialScreenW, ~top: 0.025 * initialScreenH, ~right: 0.025 * initialScreenW, ~bottom: 0.025 * initialScreenH)


class Tab(scrollBarPos, name):
  method pict(canvas_size, picts, tab_screen_width):
    let mini_slide_size = tab_screen_width * 0.7 * canvas_size.width
    let plus_slide = scale_to_width(mini_slide(text("+")), mini_slide_size)

    let combined:
      for values(res = nothing):
        each:
          pict in (picts ++ [plus_slide])
        stack(res, scale_to_width(mini_slide(pict), mini_slide_size))

    [combined, plus_slide]

  method draw(dc, picts, tab_screen_width):
    // draw the pict on the left side of the screen
    this.pict(dc.size, picts, tab_screen_width)[0].draw(dc)

class UI(selected, config, original_config_stx, tabs, tab_screen_width, selected_tab, current_slide_index):
  method draw(dx, inputState, current_rendered):
    if this.selected:
    | block:
        let selected_pict = find_pict_from_gpi(current_rendered.selected_slide, this.selected)
        let (x, y) = Find(selected_pict, ~horiz: #'left, ~vert: #'top).in(current_rendered.selected_slide)
        let width = selected_pict.width
        let height = selected_pict.height
        rectangle(~width: width, ~height: height, ~fill: #false, ~line: "blue", ~line_width: 4).pad(~left: x, ~top: y).draw(dx)
    | #false

    // todo draw selected tab
    this.tabs[this.selected_tab].draw(dx, current_rendered.slides, this.tab_screen_width)

  method slides_tab():
    for any:
      each tab in this.tabs
      if tab.name == "slides"
      | tab
      | #false

  // return a new UI after a mouse event
  method on_mouse(ev, current_rendered, canvas_size):
    let slides_tab = this.slides_tab()
    let [tab_pict, plus_pict] = slides_tab.pict(canvas_size, current_rendered.slides, this.tab_screen_width)
    if ev.kind == gui.MouseEvent.Kind.left_down:
    | block:
        let clicked:
          for any:
            each:
              pict in current_rendered.slides
              i in 0..
            println("checking " +& i)
            if is_pict_clicked(tab_pict, pict, ev)
            | i
            | #false
        println("clicked: " +& clicked)
        if clicked:
        | this with (current_slide_index = clicked)
        | if is_pict_clicked(tab_pict, plus_pict, ev)
          | this with (current_slide_index = current_rendered.slides.length())
          | this
    | this

export:
  initialScreenW
  initialScreenH
  aspect_ratio
  UI
  Tab
  scale_to_fit
